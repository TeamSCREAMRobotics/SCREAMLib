plugins {
    id 'java'
    id "edu.wpi.first.GradleRIO" version "2026.1.1"
    id 'maven-publish'
    id 'java-library'
}

def ver = '26.0.0'

version = ver
group = 'com.github.TeamSCREAMRobotics'

ext {
    verNum = ver
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://frcmaven.wpi.edu/artifactory/release" }
}

ext.localMavenURL = file("${project.buildDir}/repo/maven")

dependencies {
        annotationProcessor wpi.java.deps.wpilibAnnotations()
        implementation wpi.java.deps.wpilib()

        implementation 'com.github.jonahsnider:doglog:2026.2.0'
        implementation 'com.pathplanner.lib:PathplannerLib-java:2026.1.1'
        implementation 'com.ctre.phoenix6:wpiapi-java:26.1.0'
        implementation 'edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:2026.+'

        nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
        nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
        simulationDebug wpi.sim.enableDebug()

        nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
        nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
        simulationRelease wpi.sim.enableRelease()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.github.TeamSCREAMRobotics'
            artifactId = 'SCREAMLib'
            version = ver
		    from components.java

            pom {
                name = 'SCREAMLib'
                description = 'SCREAM Robotics Java Library'
                url = 'https://github.com/TeamSCREAMRobotics/SCREAMLib'
                }
        }
    }
    repositories{
        maven {
            name = "localDir"
            url = "file://${project.buildDir}/repo/maven"
        }
        mavenLocal()
    }
}

task printGradleVariable {
        doLast {
            println "::set-output name=gradle_var::${project.verNum}"
        }
    }

task cleanRepo(type: Delete) {
    delete localMavenURL
}

clean.dependsOn cleanRepo

task prepareVendorDep(type: Copy) {
    from 'vendordeps'
    into 'build/repo'
    include '*.json'
}

build.dependsOn(prepareVendorDep, publishToMavenLocal)

publish.dependsOn build

tasks.withType(JavaCompile) {
    options.compilerArgs.add '-parameters'
    options.encoding = 'UTF-8'
    options.fork = true
}
